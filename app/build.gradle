apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

// Load Jacoco Only if needed
if (jacocoEnabled.toBoolean()) {
    project.logger.lifecycle('Applying jacoco build file: jacoco.gradle')
    apply from: rootProject.file("jacoco.gradle")
}

// General switch for tests types this control report generation but not yet unit test selection - TODO Make this a flag
def carvedTests = project.hasProperty('carvedTests')// ? project.getProperty('carvedTests') : false //Default value false

def carvedTestSrcDir = "src/carvedTest"
def testReportPrefix = "${buildDir}/carvedTest/testExecution"
def coverageReportPrefix = "${buildDir}/carvedTest/coverage"

if (carvedTests) {
    if (project.hasProperty('testName')) {
        def testName = project.getProperty('testName')
        println ""
        println "* Processing Carved Test ${testName}"
        println ""

        carvedTestSrcDir = "src/carvedTest/${testName}"

        testReportPrefix = "${buildDir}/carvedTest/${testName}/testExecution"
        coverageReportPrefix = "${buildDir}/carvedTest/${testName}/coverage"
    }
}

android {

    // Change the src folder for carved tests
    sourceSets {
        test {
            if (carvedTests) {
                java.srcDirs = ["$carvedTestSrcDir"]
            }
        }
    }

    compileSdkVersion 28
    buildToolsVersion rootProject.buildToolsVersion
    defaultConfig {
        applicationId "com.nexus.nsnik.randomno"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 9
        versionName "3.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        /*splits {
            abi {
                enable true
                reset()
                include "x86_64", "x86", "armeabi", "armeabi-v7a", "arm64-v8a", "mips"
                universalApk false
            }
            density {
                enable true
                reset()
                include "ldpi", "mdpi", "hdpi", "xhdpi", "xxhdpi", "xxxhdpi"
            }
        }*/
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    buildTypes {
        debug {
            //applicationIdSuffix '.debug'
            //versionNameSuffix '-DEBUG'
            //ext.alwaysUpdateBuildId = false
            testCoverageEnabled jacocoEnabled.toBoolean()
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    if (project.hasProperty('devBuild')) {
        splits.density.enable = false
        splits.abi.enable = false
        aaptOptions {
            cruncherEnabled = false
        }
    }
    allprojects {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Xlint:cast" << "-Xlint:empty"
        }
    }
    testOptions {
        //This option from the original build file leads to gradle reporting no tests found.
        //execution 'ANDROID_TEST_ORCHESTRATOR'
        unitTests.all {
            // Nices output on the console/logs
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true

                test {
                    //Get last path of default set path.
                    def junitXmlTestTaskName = file("$reports.junitXml.destination").name
                    def htmlTestTaskName = file("$reports.html.destination").name

                    if (carvedTests) {
                        // Just use the same?
                        reports.junitXml.destination = file("$testReportPrefix/$junitXmlTestTaskName")
                        reports.html.destination = file("$testReportPrefix/$junitXmlTestTaskName")
                    }

                    filter {
                        //specific test method
                        if (project.hasProperty('carvedTestsFilter')) {
                            includeTestsMatching project.carvedTestsFilter
                        }
                    }
                }
            }
        }

    }
}

/*
 * Copyright 2017 nsnikhil
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

/*ext.abiCodes = ['armeabi': 3, 'armeabi-v7a': 4, 'arm64-v8a': 5, mips: 6, 'x86': 7, 'x86_64': 8]
ext.densityCodes = ['ldpi': 3, 'mdpi': 4, 'hdpi': 5, 'xhdpi': 6, 'xxhdpi': 7, 'xxxhdpi': 8]
import com.android.build.OutputFile

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
        def baseDensityVersionCode = project.ext.densityCodes.get(output.getFilter(OutputFile.DENSITY))
        if (baseAbiVersionCode != null && baseDensityVersionCode != null) {
            output.versionCodeOverride = baseAbiVersionCode * 1000 + baseDensityVersionCode * 1000 + variant.versionCode
        }
    }
}*/

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //CRASHLYTICS
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }

    //APPCOMPAT LIBRARY
    implementation 'androidx.appcompat:appcompat:1.0.0'

    //RECYCLER VIEW
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    //ANNOTATION SUPPORT LIBRARY
    implementation 'androidx.annotation:annotation:1.0.0'

    //DESIGN SUPPORT LIBRARY
    implementation 'com.google.android.material:material:1.0.0'

    //CONSTRAINT LAYOUT
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    //FIRE BASE
    implementation "com.google.firebase:firebase-core:$rootProject.fireBaseVersion"

    //RX JAVA & ANDROID
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxJavaVersion"

    //RX BINDING
    implementation "com.jakewharton.rxbinding2:rxbinding:$rootProject.rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:$rootProject.rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:$rootProject.rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-design:$rootProject.rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rootProject.rxBindingVersion"

    //TIMBER LOGGER
    implementation "com.jakewharton.timber:timber:$rootProject.timberVersion"

    //BUTTER KNIFE
    implementation "com.jakewharton:butterknife:$rootProject.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterKnifeVersion"

    //LEAK CANARY
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$rootProject.leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.leakCanaryVersion"

    //LIFE CYCLE EXTENSIONS
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    //LIFE CYCLE JAVA 8
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.0.0'

    //LIVE DATA BACKGROUND TEST
    testImplementation 'androidx.arch.core:core-testing:2.0.0'

    //LIVE DATA WITH REACTIVE STREAMS
    implementation 'androidx.lifecycle:lifecycle-reactivestreams:2.0.0'

    //MOCKITO
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"

    //JETBRAINS ANNOTATION
    implementation "org.jetbrains:annotations:$rootProject.jetBrainsAnnotationVersion"

    //ROLL-BAR
    implementation "com.rollbar:rollbar-android:$rootProject.rollBarVersion"

    //BLOCK CANARY
    implementation "com.github.markzhai:blockcanary-android:$rootProject.blockCanaryVersion"

    //AUTO VALUE
    compileOnly "com.google.auto.value:auto-value:$rootProject.autoValueVersion"
    annotationProcessor "com.google.auto.value:auto-value:$rootProject.autoValueVersion"

    //JUNIT 4
    testImplementation "junit:junit:$rootProject.jUnitVersion"

    //ANDROID JUNIT RUNNER AND JUNIT RULES
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestUtil 'androidx.test:orchestrator:1.1.1'

    //ESPRESSO
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.0'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.1.0'
    //androidTestImplementation "com.android.support.test.espresso:espresso-accessibility:$rootProject.espressoVersion"
    implementation 'androidx.test.espresso:espresso-idling-resource:3.1.0'

    //ROBO-ELECTRIC
    testImplementation "org.robolectric:robolectric:$rootProject.roboElectricVersion"


    testImplementation 'androidx.test:core:1.3.0'
}

apply plugin: 'com.google.gms.google-services'